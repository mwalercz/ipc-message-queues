%scanner Scanner.h
%print-tokens
%debug
%error-verbose
%baseclass-preinclude ../Message.hpp
%token INTEGER STRING FLOAT SIGN
%token INTEGER_VAL STRING_VAL FLOAT_VAL
%token OUTPUT READ INPUT
%token COMA, DCOLON, STAR
%polymorphic String: std::string;
             Int: int;
             Float: float;
             Boolean: bool;
             Element: Element*;
             Tuple: Tuple*;
             Comparison: Element::Comparison;

%type <String> string_val
%type <Int> integer_val
%type <Float> float_val
%type <Element> tuple_element
%type <Tuple> tuple
%type <Boolean> query_head
%type <Comparison> comparison

%%
message:
    query_head parts
    {
        this->result_ = new Query(msgPid_, time_, timeDuration_, typeCalculator_.getType(), queryParts_, $1);
    }
    | tuple
    {
        Message* message = new Output(*$1, msgPid_, time_, timeDuration_);
        delete $1;
        this->result_ = message;
    }

;


query_head:
    INPUT
    {
        $$ = false;
    }
    | READ
    {
        $$ = true;
    }
;

parts:
    part
    | parts COMA part
;

part:
    INTEGER DCOLON STAR
    {
        Element element(0);
        typeCalculator_.calculate(element);
    }
    | FLOAT DCOLON STAR
    {
        float flo(0.0);
        Element element(flo);
        typeCalculator_.calculate(element);
    }
    | STRING DCOLON STAR
    {
        std::string str("");
        Element element(str);
        typeCalculator_.calculate(element);
    }
    | INTEGER DCOLON comparison integer_val
    {
        Element element($4);
        typeCalculator_.calculate(element);
        QueryPart qp(queryParts_.size(), element, $3);
        queryParts_.push_back(qp);
    }
    | FLOAT DCOLON comparison float_val
    {
         Element element($4);
         typeCalculator_.calculate(element);
         QueryPart qp(queryParts_.size(), element, $3);
         queryParts_.push_back(qp);
    }
    | STRING DCOLON comparison string_val
    {
         Element element($4);
         typeCalculator_.calculate(element);
         QueryPart qp(queryParts_.size(), element, $3);
         queryParts_.push_back(qp);
    }
;

comparison:
    SIGN
    {
        std::string matched = std::string(d_scanner.matched());
        if (matched == ">")
            $$ = Element::Comparison::kGreater;
        if (matched == ">=")
            $$ = Element::Comparison::kGreaterOrEqual;
        if (matched == "=")
            $$ = Element::Comparison::kEqual;
        if (matched == "<")
            $$ = Element::Comparison::kLower;
        if (matched == "<=")
            $$ = Element::Comparison::kLowerOrEqual;
    }
;

integer_val:
    INTEGER_VAL
    {
        $$ = int(std::stoi(d_scanner.matched()));
    }
;

float_val:
    FLOAT_VAL
    {

        $$ = float(std::stof(d_scanner.matched()));
    }
;

string_val:
    STRING_VAL
    {
        $$ = std::string(d_scanner.matched());
    }
;

tuple:
    tuple_head tuple_elements
    {
        $$ = new Tuple(tupleElements_);

    }


;

tuple_head:
    OUTPUT
;

tuple_elements:
    tuple_element
    {
        tupleElements_.push_back(*$1);
        delete $1;


    }
    | tuple_elements COMA tuple_element
    {
        tupleElements_.push_back(*$3);
        delete $3;

    }
;

tuple_element:
    float_val
    {
       $$ = new Element($1);
    }

    | integer_val
    {
       $$ = new Element($1);
    }

    | string_val
    {
       $$ = new Element($1);
    }

;
